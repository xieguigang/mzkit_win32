imports "netCDF.utils" from "base";

# demo data comes from the mzkit internal demo file
using cdf as open.netCDF(file = "C:\\Program Files\\BioNovoGene\\mzkit_win32\\demo\\200ppm-scan.CDF") {
    #' get vector variable information
    #' and then read vector data from it
    const readVector = function(name) {
        let varinf = var(cdf, name = name);
        let vector = getValue(varinf);

        vector;
    }
    # create a dataframe for inspect our raw data which
    # we have read before
    let TIC = data.frame(
        scan_time = readVector("scan_acquisition_time"), 
        totalIons = readVector("total_intensity")
    );

    print(TIC, max.print = 13);

    # do ggplot of TIC
    require(ggplot);

    bitmap(file = `${@dir}/tic_ggplot.png`, width = 2400, height = 1440) {
        ggplot(TIC, aes(x = "scan_time", y = "totalIons"), padding = "padding: 200px 150px 200px 300px;")
        + geom_line(width = 7)
        + ggtitle("Total Ions Chromatographic(TIC)")
        + scale_x_continuous(labels = "F2")
        + scale_y_continuous(labels = "G2")
        ;
    }

    require(mzkit);
    #' A chromatogram is the trace generated by the detector
    imports "chromatogram" from "mzkit";
    imports "visual" from "mzplot";
    # convert the TIC raw data to chromatogram object
    # and then plot the chromatogram to image file
    tic = toChromatogram(as.chromatogram(TIC$scan_time, TIC$totalIons));

    bitmap(file = `${@dir}/tic_mzkit.png`, width = 2400, height = 1440) {
        plot(chromatogram::overlaps(list(demo_data = tic)), show.labels = FALSE);
    }
}